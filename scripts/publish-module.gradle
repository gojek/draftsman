apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                //artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Draftsman'
                    url = 'https://github.com/gojek/draftsman'
                    licenses {
                        license {
                            name = 'Draftsman License'
                            url = 'https://github.com/gojek/draftsman/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'jitinsharma'
                            name = 'Jitin Sharma'
                            email = 'jitin.sharma@gojek.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/gojek/draftsman.git'
                        developerConnection = 'scm:git:ssh://github.com/gojek/draftsman.git'
                        url = 'https://github.com/gojek/draftsman/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
